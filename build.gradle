import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease
plugins {
    id("java")
    id 'org.jetbrains.intellij.platform' version '2.2.1'
}

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}
group = "org.hai.work"
version = "1.3-SNAPSHOT"
dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2024.3.2.1'
    }
    implementation('org.springframework.ai:spring-ai-openai:1.0.0-M5') {
        exclude group: 'com.fasterxml.jackson.core'
        exclude group: 'com.fasterxml.jackson.databind'
        exclude group: 'com.fasterxml.jackson.annotation'
    }
    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'commons-collections:commons-collections:3.2.2'
}

// IntelliJ 插件配置
intellijPlatform {
    pluginVerification {
        ides {
            recommended()
            select {
                it.types = [IntelliJPlatformType.PhpStorm]
                it.channels = [ProductRelease.Channel.RELEASE]
                it.sinceBuild = '233'
                it.untilBuild = '243.*'
            }
        }
    }
    pluginConfiguration {
        ideaVersion {
            sinceBuild = '233'
            untilBuild = '243.*'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation', '-parameters']
}
